// Begin YW25A

// Graphics

// Purchase Menu

spriteset (spriteset_yw25a_purchase_original, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_purchase ("coaches/25a", "yw25a")
}

// Vehicle
spriteset (spriteset_yw25a_original, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("coaches/25a", "yw25a")
}

switch (FEAT_TRAINS, SELF, switch_yw25a_graphics, position_in_articulated_veh % 3) {
    1: spriteset_yw25a_original;
    empty_sprites;
}


switch (FEAT_TRAINS, SELF, switch_yw25a_articulated_part, extra_callback_info1) {
    1..2: return yw25a;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

//Cap
switch (FEAT_TRAINS, SELF, switch_yw25a_capacity, position_in_articulated_veh % 3) {
    1: return 66;
    return 0;
}

item (FEAT_TRAINS, yw25a) {
    property {
        // Menu
        name:                           string(STR_NAME_YW25A);
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        misc_flags:                     bitmask(TRAIN_FLAG_AUTOREFIT);
        variant_group:					yz25a;
        // Availability
        climates_available:             ALL_CLIMATES;
        introduction_date:              date(1989,01,01);
        model_life:                     VEHICLE_NEVER_EXPIRES;
        vehicle_life:                   30;
        track_type:                     SACN;

        // Cargo
        refittable_cargo_classes:       bitmask(CC_PASSENGERS);
        non_refittable_cargo_classes:   0;
        cargo_allow_refit:              [PASS, TOUR, YETI, YETY];
        cargo_disallow_refit:           [];
        refit_cost:                     0;
        default_cargo_type:             PASS;
        ai_special_flag:                AI_FLAG_PASSENGER;
        cargo_capacity:                 66;
        loading_speed:                  8 << param_loading_speed;
        cargo_age_period:               370 << param_cargo_decay;

        // Stats
        speed:                          120 km/h;
        power:                          0 hp;
        weight:                         48.5 ton;
        cost_factor:                    8;
        running_cost_base:              RUNNING_COST_ELECTRIC;
        running_cost_factor:            6;
    }
    graphics {
        // Menu
        additional_text:                string(STR_DESC_2, string(STR_FULL_NAME_YW25A), string(STR_COMFORT_HIGH, 288));
        can_attach_wagon:               locowagon;

        // Graphics
        default:                        switch_yw25a_graphics;
        purchase:                       spriteset_yw25a_purchase_original;
        articulated_part:               switch_yw25a_articulated_part;

        // Stats
        length:                         switch_length_2_8_2;
        cargo_capacity:                 switch_yw25a_capacity;
        cargo_age_period:               (10 * acdecayfactor() * cafedecayfactor()) << param_cargo_decay;
        cost_factor:                    9;
        running_cost_factor:            7 * runningcostfactor() * cafecostfactor() + accostfactor();
        purchase_running_cost_factor:   28;
        purchase_cargo_capacity:        66/3;
        name:                           switch_25a_name;
    }
}

// End YW25A
