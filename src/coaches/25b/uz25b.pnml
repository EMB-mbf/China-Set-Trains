//Begin UZ25B
switch (FEAT_TRAINS, SELF, switch_uz25b_name, getbits(extra_callback_info1, 0, 8) == 0x20? getbits(extra_callback_info1, 8, 8) : 0xFFFF) {
    0: return string(STR_25B_SERIES);
    return CB_RESULT_NO_TEXT;
}


// Graphics

// Purchase Menu
spriteset (spriteset_uz25b_purchase_original, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_purchase ("coaches", "25b/uz25b")
}

// Vehicle
spriteset (spriteset_uz25b_original, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("coaches", "25b/uz25b")
}
spriteset (spriteset_uz25b_green, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("coaches", "25b/uz25b-green")
}
spriteset (spriteset_uz25b_baotou, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("coaches/25b", "uz25b-hhb")
}
spriteset (spriteset_uz25b_jixu, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("coaches/25b", "uz25b-jnx")
}
spriteset (spriteset_uz25b_qingdao, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("coaches/25b", "uz25b-jqd")
}
spriteset (spriteset_uz25b_shenji, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("coaches/25b", "uz25b-ssj")
}
spriteset (spriteset_uz25b_dalian, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("coaches/25b", "uz25b-sdl")
}
spriteset (spriteset_uz25b_changchun, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("coaches/25b", "uz25b-scc")
}
spriteset (spriteset_uz25b_harbin, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("coaches/25b", "uz25b-sks")
}
spriteset (spriteset_uz25b_mujia, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("coaches/25b", "uz25b-hjm")
}

switch (FEAT_TRAINS, SELF, switch_uz25b_articulated_part, extra_callback_info1) {
    1..2: return uz25b;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

switch (FEAT_TRAINS, SELF, switch_uz25b_cargo_subtype, cargo_subtype) {
    0: return string(STR_BUILD_YEAR);
    1: return string(STR_ORIGINAL);
    2: return string(STR_GAOZU_GREEN);
    3: return string(STR_BAOTOU);
    4: return string(STR_JN_XZ);
    5: return string(STR_QINGDAO);
    6: return string(STR_SY_JL_COLD);
    7: return string(STR_DALIAN_COLD);
    8: return string(STR_CHANGCHUN_COLD);
    9: return string(STR_HARBIN_COLD);
    10: return string(STR_MTK_KMS_COLD);
    return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, switch_uz25b_graphics_build_year_depend, build_year) {
    0..2014: spriteset_uz25b_original;
    spriteset_uz25b_green;
}

switch (FEAT_TRAINS, SELF, switch_uz25b_cargo_subtype_0, param_gaozulv) {
    1: switch_uz25b_graphics_build_year_depend;
    spriteset_uz25b_original;
}
switch (FEAT_TRAINS, SELF, switch_uz25b_graphics_subtype, cargo_subtype) {
    0:switch_uz25b_cargo_subtype_0;
    1: spriteset_uz25b_original;
    2: spriteset_uz25b_green;
    3: spriteset_uz25b_baotou;
    4: spriteset_uz25b_jixu;
    5: spriteset_uz25b_qingdao;
    6: spriteset_uz25b_shenji;
    7: spriteset_uz25b_dalian;
    8: spriteset_uz25b_changchun;
    9: spriteset_uz25b_harbin;
    10: spriteset_uz25b_mujia;
}

switch (FEAT_TRAINS, SELF, switch_uz25b_graphics, position_in_articulated_veh % 3) {
    1: switch_uz25b_graphics_subtype;
    empty_sprites;
}

switch (FEAT_TRAINS, SELF, switch_uz25b_capacity, position_in_articulated_veh % 3) {
    1: return 72;
    return 0;
}


item (FEAT_TRAINS, uz25b) {
    property {
        // Menu
        name:                           string(STR_NAME_UZ25B);
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        misc_flags:                     bitmask(TRAIN_FLAG_AUTOREFIT);
        variant_group:					2077;

        // Availability
        climates_available:             ALL_CLIMATES;
        introduction_date:              date(1992,01,01);
        model_life:                     30;
        vehicle_life:                   30;
        track_type:                     SACN;

        // Cargo
        refittable_cargo_classes:       bitmask(CC_MAIL, CC_ARMOURED);
        non_refittable_cargo_classes:   0;
        cargo_allow_refit:              [MAIL, VALU, GOLD, DIAM];
        cargo_disallow_refit:           [];
        refit_cost:                     0;
        default_cargo_type:             MAIL;
        ai_special_flag:                AI_FLAG_CARGO;
        cargo_capacity:                 68;
        loading_speed:                  6 << param_loading_speed;
        cargo_age_period:               185 << param_cargo_decay;

        // Stats
        speed:                          120 km/h;
        power:                          0 hp;
        weight:                         46.8 ton;
        cost_factor:                    8;
        running_cost_base:              RUNNING_COST_ELECTRIC;
        running_cost_factor:            6;
    }
    graphics {
        // Menu
        additional_text:                string(STR_DESC_3, string(STR_FULL_NAME_UZ25B), string(STR_LV_AVAILABLE_L ,string(STR_LIVERY_AVAILABLE), string(STR_ORIGINAL), string(STR_GAOZU_GREEN), string(STR_BAOTOU), string(STR_JN_XZ), string(STR_QINGDAO), string(STR_SY_JL_COLD), string(STR_DALIAN_COLD), string(STR_CHANGCHUN_COLD), string(STR_HARBIN_COLD), string(STR_MTK_KMS_COLD)), string(STR_FRESHNESS_MEDIUM, 200));
        can_attach_wagon:               locowagon;
        cargo_subtype_text:             switch_uz25b_cargo_subtype;

        // Graphics
        default:                        switch_uz25b_graphics;
        purchase:                       spriteset_uz25b_purchase_original;
        articulated_part:               switch_uz25b_articulated_part;

        // Stats
        length:                         switch_length_2_8_2;
        cargo_capacity:                 switch_uz25b_capacity;
        cargo_age_period:               200 << (param_cargo_decay + param_wagoncargodecay);
        cost_factor:                    7;
        running_cost_factor:            5 * runningcostfactor() * cafecostfactor();
        purchase_running_cost_factor:   20;
        purchase_cargo_capacity:        72/3;
        name:                           switch_uz25b_name;

    }
}

// End UZ25B
