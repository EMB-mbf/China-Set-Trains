//Begin CA25B_AC
switch (FEAT_TRAINS, SELF, switch_ca25b_ac_name, getbits(extra_callback_info1, 0, 8) == 0x20? getbits(extra_callback_info1, 8, 8) : 0xFFFF) {
    0: return string(STR_25B_SERIES);
    return CB_RESULT_NO_TEXT;
}

// Graphics

// Purchase Menu
spriteset (spriteset_ca25b_ac_purchase_original, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_purchase ("coaches/25b", "ca25b-hhb")
}

// Vehicle
spriteset (spriteset_ca25b_ac_baotou, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("coaches/25b", "ca25b-hhb")
}
spriteset (spriteset_ca25b_ac_jixu, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("coaches/25b", "ca25b-jnx")
}
spriteset (spriteset_ca25b_ac_qingdao, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("coaches/25b", "ca25b-jqd")
}
spriteset (spriteset_ca25b_ac_shenji, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("coaches/25b", "ca25b-ssj")
}
spriteset (spriteset_ca25b_ac_dalian, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("coaches/25b", "ca25b-sdl")
}
spriteset (spriteset_ca25b_ac_changchun, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("coaches/25b", "ca25b-scc")
}
spriteset (spriteset_ca25b_ac_harbin, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("coaches/25b", "ca25b-sks")
}
spriteset (spriteset_ca25b_ac_mujia, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("coaches/25b", "ca25b-hjm")
}
spriteset (spriteset_ca25b_ac_jitong, ZOOM_LEVEL_IN_4X, BIT_DEPTH_32BPP) {
    template_standard ("coaches/25b", "ca25b-jitong")
}

switch (FEAT_TRAINS, SELF, switch_ca25b_ac_articulated_part, extra_callback_info1) {
    1..2: return ca25b_ac;
    return CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}

switch (FEAT_TRAINS, SELF, switch_ca25b_ac_cargo_subtype, cargo_subtype) {
    0: return string(STR_BAOTOU);
    1: return string(STR_JN_XZ);
    2: return string(STR_QINGDAO);
    3: return string(STR_SY_JL_COLD);
    4: return string(STR_DALIAN_COLD);
    5: return string(STR_CHANGCHUN_COLD);
    6: return string(STR_HARBIN_COLD);
    7: return string(STR_MTK_KMS_COLD);
    8: return string(STR_JITONG);
    return CB_RESULT_NO_TEXT;
}

switch (FEAT_TRAINS, SELF, switch_ca25b_ac_graphics_subtype, cargo_subtype) {
    0: spriteset_ca25b_ac_baotou;
    1: spriteset_ca25b_ac_jixu;
    2: spriteset_ca25b_ac_qingdao;
    3: spriteset_ca25b_ac_shenji;
    4: spriteset_ca25b_ac_dalian;
    5: spriteset_ca25b_ac_changchun;
    6: spriteset_ca25b_ac_harbin;
    7: spriteset_ca25b_ac_mujia;
    8: spriteset_ca25b_ac_jitong;
}

switch (FEAT_TRAINS, SELF, switch_ca25b_ac_graphics, position_in_articulated_veh % 3) {
    1: switch_ca25b_ac_graphics_subtype;
    empty_sprites;
}

switch (FEAT_TRAINS, SELF, switch_ca25b_ac_capacity, position_in_articulated_veh % 3) {
    1: return 48;
    return 0;
}


item (FEAT_TRAINS, ca25b_ac ) {
    property {
        // Menu
        name:                           string(STR_NAME_CA25B_AC);
        sprite_id:                      SPRITE_ID_NEW_TRAIN;
        misc_flags:                     bitmask(TRAIN_FLAG_AUTOREFIT);
        variant_group:					2077;

        // Availability
        climates_available:             ALL_CLIMATES;
        introduction_date:              date(1993,01,01);
        model_life:                     30;
        vehicle_life:                   30;
        track_type:                     SACN;

        // Cargo
        refittable_cargo_classes:       bitmask(CC_PASSENGERS);
        non_refittable_cargo_classes:   0;
        cargo_allow_refit:              [PASS, TOUR, YETI, YETY];
        cargo_disallow_refit:           [];
        refit_cost:                     0;
        default_cargo_type:             PASS;
        ai_special_flag:                AI_FLAG_PASSENGER;
        cargo_capacity:                 118;
        loading_speed:                  4 << param_loading_speed;
        cargo_age_period:               185 << param_cargo_decay;

        // Stats
        speed:                          120 km/h;
        power:                          0 hp;
        weight:                         44.5 ton;
        cost_factor:                    8;
        running_cost_base:              RUNNING_COST_ELECTRIC;
        running_cost_factor:            6;
        bitmask_vehicle_info:           bitmask(1);
    }
    graphics {
        // Menu
        additional_text:                string(STR_DESC_4, string(STR_FULL_NAME_CA25B_AC), string(STR_CAFE_EFFECT), string(STR_LV_AVAILABLE_S ,string(STR_LIVERY_AVAILABLE), string(STR_BAOTOU), string(STR_JN_XZ), string(STR_QINGDAO), string(STR_SY_JL_COLD), string(STR_DALIAN_COLD), string(STR_CHANGCHUN_COLD), string(STR_HARBIN_COLD), string(STR_MTK_KMS_COLD), string(STR_JITONG)), string(STR_COMFORT_MEDIUM, 180));
        can_attach_wagon:               locowagon;
        cargo_subtype_text:             switch_ca25b_ac_cargo_subtype;

        // Graphics
        default:                        switch_ca25b_ac_graphics;
        purchase:                       spriteset_ca25b_ac_purchase_original;
        articulated_part:               switch_ca25b_ac_articulated_part;

        // Stats
        length:                         switch_length_2_8_2;
        cargo_capacity:                 switch_ca25b_ac_capacity;
        cargo_age_period:               (9 * acdecayfactor() * cafedecayfactor()) << param_cargo_decay;
        cost_factor:                    22;
        running_cost_factor:            4 * runningcostfactor() + accostfactor() + 50; // IT IS A RESTAURANT CAR!
        purchase_running_cost_factor:   58;
        purchase_cargo_capacity:        48/3;
        name:                           switch_ca25b_ac_name;

    }
}

// End CA25B_AC
